buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/micronaut/core-releases-local" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build:micronaut-gradle-plugins:2.0.0.RC10"
    }
}

subprojects { Project subproject ->

    repositories {
        maven { url "https://oss.jfrog.org/oss-snapshot-local" }
    }
    group "io.micronaut.views"

    if (subproject.name != 'views') {
        apply plugin: "io.micronaut.build.common"

        micronautBuild {
            enforcedPlatform = true
        }
    } else {
        apply plugin: "java-platform"
        version projectVersion
    }

    if (subproject.parent.name == "docs-examples" || subproject.name == "docs-examples") {
        return
    }

    apply plugin: "io.micronaut.build.publishing"

    if (subproject.name != 'views') {
        
        apply plugin: "io.micronaut.build.dependency-updates"
        dependencies {
            testImplementation("org.spockframework:spock-core:${spockVersion}") {
                exclude module:'groovy-all'
            }
            testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"        
        }            
    }

    project.tasks.withType(Test) {
        useJUnitPlatform()
    }    
}

apply plugin: "io.micronaut.build.docs"
apply plugin: "io.micronaut.build.dependency-updates"